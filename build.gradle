import static org.gradle.internal.os.OperatingSystem.current

group 'com.mangomelancholy'
version '0.0.1-SNAPSHOT'

task runProgram(type: JavaExec) {
    description = 'Runs the main program'
    group = 'application'

    dependsOn ':jni-bindings:build', ':jni-implementation:build'
    mainClass = 'com.mangomelancholy.llama.cpp.java.bindings.Main'
//    systemProperty 'java.library.path', 'jni-implementation/build/lib/main/debug'
    systemProperty 'java.library.path', 'jni-implementation/build/cmake/main/cpp/Debug'

    final llamaRelativePath = current().isWindows() ? '/jni-implementation/build/cmake/bin/Debug'
            : '/jni-implementation/build/cmake/debug'
    final String llamaAbsolutePath = projectDir as String + llamaRelativePath

    final envVariableName = current().isWindows() ? 'PATH' :
            current().isMacOsX() ? 'DYLD_LIBRARY_PATH' : 'LD_LIBRARY_PATH'
    final envVariableValue = System.getenv(envVariableName) ? System.getenv(envVariableName) : ''
    environment envVariableName, "${envVariableValue}${File.pathSeparator}${llamaAbsolutePath}"

    doFirst {
        classpath = files(project(':jni-bindings').sourceSets.main.runtimeClasspath)
    }
}