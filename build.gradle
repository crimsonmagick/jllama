import static org.gradle.internal.os.OperatingSystem.current

group 'com.mangomelancholy'
version '0.0.1-SNAPSHOT'

task runProgram(type: JavaExec) {
    description = 'Runs the main program'
    group = 'application'

    dependsOn ':jni-bindings:build', ':jni-implementation:build'
    mainClass = 'com.mangomelancholy.llama.cpp.java.bindings.Main'
    systemProperty 'java.library.path', 'jni-implementation/build/lib/main/debug'

    final isWindows = current().isWindows()
    final llamaRelativePath = isWindows ? '/build/cmake/release/bin'
            : '/build/cmake/release'
    final String llamaAbsolutePath = projectDir as String + llamaRelativePath

    final envVariableName = isWindows ? 'PATH' : 'LD_LIBRARY_PATH'
    final envVariableValue = System.getenv(envVariableName) ? System.getenv(envVariableName) : ''
    environment envVariableName, "${envVariableValue}${File.pathSeparator}${llamaAbsolutePath}"

    doFirst {
        classpath = files(project(':jni-bindings').sourceSets.main.runtimeClasspath)
    }
}